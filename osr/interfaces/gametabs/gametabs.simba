(*
# GameTabs
Methods to interact with the gametabs.
*)
{$DEFINE SRL_GAMETABS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

type
  ERSGameTab = (
    COMBAT, STATS, QUESTS, INVENTORY, EQUIPMENT, PRAYER, MAGIC,
    CLAN, FRIENDS, ACCOUNT, LOGOUT, OPTIONS, EMOTES, MUSIC, UNKNOWN
  );

  TRSGameTabs = record(TRSInterface)
    Boxes: array [ERSGameTab] of TBox;
  end;

procedure TRSGameTabs.SetTabBoxes();
var
  tmp: TBoxArray;
  i: Int32;
begin
  case RSClient.Mode of
    ERSClientMode.FIXED, ERSClientMode.RESIZABLE_CLASSIC:
    begin
      tmp := Grid(7, 2, 30, 33, [3, 265], [Self.X1 + 6, Self.Y1 + 1]);
      for i := 0 to High(tmp) do
        Self.Boxes[ERSGameTab(i)] := tmp[i];
    end;

    ERSClientMode.RESIZABLE_MODERN:
      begin
        // One row
        if (RootInterface.Width >= 948) then
        begin
          tmp := Grid(14, 1, 30, 33, [3, 0], [Self.X1 - 197, Self.Y2 - 70]);

          for i := 0 to Ord(ERSGameTab.MAGIC) do
            Self.Boxes[ERSGameTab(i)] := tmp[i];

          for i := Ord(ERSGameTab.MUSIC) downto Ord(ERSGameTab.FRIENDS) - 1 do
            Self.Boxes[ERSGameTab(i)] := tmp[i-1];
        end
        else
        begin
          // Two rows
          tmp := Grid(7, 2, 30, 33, [3, 3], [Self.X1, Self.Y2 - 70]);
          for i := 0 to High(tmp) do
            Self.Boxes[ERSGameTab(i)] := tmp[i];
        end;

        // Logout button is moved to top right corner
        Self.Boxes[ERSGameTab(7)] := tmp[10];
        Self.Boxes[ERSGameTab(10)] := [RootInterface.X2 - 19, RootInterface.Y1 + 9, RootInterface.X2 - 10, RootInterface.Y1 + 18];
      end;
  end;
end;

function TRSGameTabs.GetCurrentTab(): ERSGametab;
var
  tab: ERSGameTab;
begin
  if (RSClient.Mode = ERSClientMode.RESIZABLE_MODERN) and (SRL.CountColor(8639715, Self.Boxes[ERSGameTab.LOGOUT]) > 0) then
    Exit(ERSGameTab.LOGOUT);
  
  for tab := Low(ERSGameTab) to ERSGameTab.MUSIC do
    if (SRL.CountColor(CTS1(1778534, 22), Self.Boxes[tab]) > 50) then
      Exit(tab);
      
  Result := ERSGameTab.UNKNOWN;
end;

(*
## GameTabs.Open()
```pascal
function TRSGameTabs.Open(tab: ERSGameTab): Boolean;
```
Opens the specified `Tab`.

Example:
```pascal
WriteLn GameTabs.Open(ERSGameTab.INVENTORY);
```
*)
function TRSGameTabs.Open(tab: ERSGameTab): Boolean;
var
  i: Int32;
begin
  if Self.GetCurrentTab() = tab then Exit(True);

  for i := 1 to 3 do
  begin
    Mouse.Click(Self.Boxes[tab], MOUSE_LEFT, SRL.Dice(50));

    Result := WaitUntil(Self.GetCurrentTab() = tab, SRL.TruncatedGauss(50, 2000), SRL.TruncatedGauss(2000, 3000));
    if Result then Exit;
  end;
end;

procedure TRSGameTabs.Draw(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Bitmap.DrawBoxes(Self.Boxes, $00FFFF);
end;

procedure TRSGameTabs.Setup(); override;
begin
  inherited;
    
  Self.Name := 'GameTabs';
end;

procedure TRSGameTabs.SetupAlignment(); overload;
var
  alignment: TRSInterfaceAlignment;
begin
  alignment.Bottom := RootInterface.Y2;

  case RSClient.Mode of
    ERSClientMode.FIXED:
    begin
      alignment.Left := RootInterface.X2 - 242;
      alignment.Right := RootInterface.X2 - 2;
      alignment.Top := RootInterface.Y2 - 334;
    end;

    ERSClientMode.RESIZABLE_CLASSIC:
    begin
      alignment.Left := RootInterface.X2 - 240;
      alignment.Right := RootInterface.X2;
      alignment.Top := RootInterface.Y2 - 334;
    end;

    ERSClientMode.RESIZABLE_MODERN:
    begin
      alignment.Left := RootInterface.X2 - 230;
      alignment.Right := RootInterface.X2;
      alignment.Top := RootInterface.Y2 - 346;
    end;
  end;

  Self.SetupAlignment(alignment);

  //Needed because depending on the client width the tabs stack into row(s)
  if (RSClient.Mode = ERSClientMode.RESIZABLE_MODERN) and (RootInterface.Width >= 948) then
  begin
    Self.Y1 += 36;
    Self.Y2 += 36;
    Self.Bounds := Box(Self.X1, Self.Y1, Self.X2, Self.Y2);
    Self.Rectangle := Self.Bounds.ToRectangle();
    Self.Center := Self.Bounds.Middle();
    Self.Width := Self.Bounds.Width();
    Self.Height := Self.Bounds.Height();
  end;

  Self.SetTabBoxes();
end;

var
  GameTabs: TRSGameTabs;

procedure TRSClient.ClientModeChanged(); override;
begin
  inherited;
  GameTabs.SetupAlignment();
end;

procedure TSRL.Setup(); override;
begin
  inherited;
  GameTabs.Setup();
end;

procedure TSRL.Debug(bitmap: TMufasaBitmap); override;
begin
  inherited;
  GameTabs.Draw(bitmap);
end;

